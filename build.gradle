subprojects {
    apply plugin: 'signing'
    apply plugin: 'maven'
    apply plugin: 'maven-publish'
    apply plugin: 'java'

    group = 'de.tomask.gradle.jaxb'
    version = '2.3'

    ext {
       repoUrl = System.getProperty('repoUrl', "$buildDir/repo")
    }

    repositories {
        mavenCentral()
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives sourcesJar
        archives javadocJar
    }

/*
task integrationTest(type: GradleBuild, dependsOn: build) {
    dir = 'src/sample'
    tasks = ['clean', 'build']
}
*/

    signing {
        required = { gradle.taskGraph.hasTask("uploadArchives") && !version.endsWith("SNAPSHOT") }
        sign configurations.archives
    }

    ext.defaultProject = {
        name 'Gradle JAXB plugin'
        // packaging 'jar' (Packaging is set later through direct modification of the xml)
        description 'The Gradle JAXB plugin creates Java classes from XSD schemas.'
        url 'http://github.com/tomask-de/gradle-jaxb-plugin'
        inceptionYear '2011'

        scm {
            developerConnection 'git@github.com:tomask-de/gradle-jaxb-plugin.git'
            connection 'git://github.com/tomask-de/gradle-jaxb-plugin.git'
            url 'https://github.com/tomask-de/gradle-jaxb-plugin'
        }

        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                distribution 'repo'
            }
        }

        developers {
            developer {
                id 'stianh'
                name 'Stian Hegglund'
                email 'stian.hegglund@gmail.com'
                roles {
                    role 'Developer'
                }
                timezone '+1'
            }
            developer {
                id 'stigkj'
                name 'Stig Kleppe-JÃ¸rgensen'
                email 'from.gradle-jaxb@nisgits.net'
                roles {
                    role 'Developer'
                }
                timezone '+1'
            }
        }
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
            }
        }

        repositories {
            maven {
                url "$repoUrl" 
            }
        }
    }

}
